<html><head>
<title>NaviServer Tcl Overview -  </title>
<link rel="stylesheet" href=".././nsd.css" type="text/css">
</head>
<! -- Generated from file 'files/tcl-overview.html' by tcllib/doctools with format 'html'
   -->
<! -- CVS: $Id$ NaviServer Tcl Overview.n
   -->

<body>
<hr> [
  <a href=".././toc.html">Table Of Contents</a>
| <a href=".././index.html">Keyword Index</a>
] <hr>

<h1> NaviServer Tcl Overview(n) 4.99.2  &quot;&quot;</h1>
<h2><a name="name">NAME</a></h2>
<p>
<p> NaviServer Tcl Overview - NaviServer Tcl Overview



<h2><a name="table_of_contents">TABLE OF CONTENTS</a></h2>
<p>&nbsp;&nbsp;&nbsp;&nbsp;<a href="#table_of_contents">TABLE OF CONTENTS</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#description">DESCRIPTION</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#what_is_tcl?">What is Tcl?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#recommended_reading">Recommended Reading</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#why_use_tcl?">Why Use Tcl?</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#adps_and_tcl_libraries">ADPs and Tcl Libraries</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#tcl_interpreters">Tcl Interpreters</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#global_variables">Global Variables</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#sharing_files_between_interpreters">Sharing Files Between Interpreters</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#working_with_ns_set_and_form_data">Working with ns_Set and Form Data</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#ns_set_data_structure">ns_Set Data Structure</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;<a href="#tcl_examples">Tcl Examples</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#example_1:_hello">Example 1: hello</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#example_2:_showhdrs">Example 2: showhdrs</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#example_3a:_genstory">Example 3a: genstory</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#example_3b:_pagetcl/genstory">Example 3b: pagetcl/genstory</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#example_4:_redirect">Example 4: redirect</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#example_5:_desctable">Example 5: desctable</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#example_6:_getempsm">Example 6: getempsm</a><br>
&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="#example_7:_wincgi">Example 7: wincgi</a><br>
<h2><a name="description">DESCRIPTION</a></h2>
<p>

This chapter contains general information about using Tcl whether you
are using the ADP approach or the Tcl Libraries approach, both of
which use the same features of Tcl.


<p>


<h2><a name="what_is_tcl?">What is Tcl?</a></h2>
<p>


<p>


Tcl is a programming system developed by John Ousterhout at the
University of California, Berkeley. According to Dr. Ousterhout:


<p>

Tcl is a simple scripting language for controlling and extending
applications; its name stands for &quot;tool command language&quot;. Tcl
provides generic programming facilities, such as variables and loops
and procedures, that are useful in a variety of applications.
Furthermore, Tcl is embedable. Its interpreter is a library of C
procedures that can easily be incorporated into applications, and each
application can extend the core Tcl features with additional commands
for that application.

<p>


NaviServer supports the Tcl v7.6 commands. When you write NaviServer
extensions in Tcl, you use the core functionality of Tcl as well as a
set of NaviServer-specific Tcl functions. For example, there is an
NaviServer Tcl function called ns_conn that allows your Tcl script to
obtain information about the current connection. In addition,
functions are available for returning results to Web clients,
accessing databases, and managing the permissions (access control)
system.


<p>


This book describes the two different methods for extending NaviServer
with Tcl, using NaviServer Dynamic Pages (Chapter 2) and Tcl libraries
(see Chapter 3), plus general information on using Tcl with either
method (see Chapter 4). It also provides a reference for NaviServer's
Tcl API (see Chapter 5).


<p>


 <h3><a name="recommended_reading">Recommended Reading</a></h3>
<p>


<p>


For more information on the Tcl language, we recommend the following
sources:


<p>


<ul>

<li> Practical Programming in Tcl and Tk, by Brent B. Welch (Prentice Hall PTR, 1995)


<br><br>


<br><br>
<li> Tcl and the Tk Toolkit, by John K. Ousterhout (Addison-Wesley, 1994)


<br><br>


<br><br>
<li> For the complete manual pages for the Tcl v7.6 commands, visit the
     web site http://www.scriptics.com/man.


<br><br>


<br><br>
<li> Philip Greenspun's web site, http://www-swiss.ai.mit.edu/wtr/dead-trees/, which includes
     chapters from his book, Database Backed Web Sites, The Thinking Person's Guide to Web Publishing,
     and numerous examples of NaviServer Tcl database applications.


<br><br>


<br><br>
<li> The web site: http://www.yahoo.com/Computers_and_Internet/Programming_Languages/Tcl_Tk


</ul>



<p>


<h2><a name="why_use_tcl?">Why Use Tcl?</a></h2>
<p>


<p>


You can extend NaviServer using Tcl, C, or CGI. However, using Tcl has
the following advantages:


<p>

<ul>

<li> Creating new Tcl scripts is very easy. You can embed Tcl scripts
 directly into HTML pages using NaviServer Dynamic Pages (ADPs), or
 you can create .tcl files and register them to handle URLs or URL
 hierarchies.


<br><br>


<br><br>
<li> Tcl's scripting language is easier to learn and use than compiled
 programming languages. It provides general programming
 capabilities, such as variables, loops, and procedures, but it
 doesn't require strong type definitions or compilation. Plus,
 NaviServer's Tcl interface provides a complete set of Tcl
 extensions specifically oriented towards web server applications,
 such as returning content to the client, accessing form data,
 accessing databases, and logging.


<br><br>


<br><br>
<li> Many of the basic NaviServer services are implemented as Tcl
 scripts. In fact, most of the database operations such as handling
 database inserts, queries, and updates, are written as Tcl
 scripts. Not only has the Tcl interface greatly reduced the
 development time for the NaviServer engineering team, it also
 allows you to easily modify the basic database operations by
 simply editing the Tcl scripts that implement the functionality.


<br><br>


<br><br>
<li> The Tcl and C interfaces typically provide better performance than
 CGI. However, if you have existing CGI programs, you may want to
 use NaviServer's CGI interfaces to take advantage of
 previously-existing code.


</ul>



<p>


The C interface for NaviServer is described in the NaviServer C
Developer's Guide, and the CGI interface is desribed in the NaviServer
Administrator's Guide.


<p>



<h2><a name="adps_and_tcl_libraries">ADPs and Tcl Libraries</a></h2>
<p>


<p>


There are two ways to extend NaviServer using Tcl, and each is
better-suited to different situations:


<p>


<ul>

<li> NaviServer Dynamic Pages (ADPs): ADPs allow you to embed Tcl
 scripts directly into HTML pages. The script(s) are interpreted
 dynamically when the page is accessed. ADPs are ideal in
 situations where you want to generate all or part of a specific
 page dynamically. You can re-use code by storing Tcl scripts in
 Tcl libraries and invoking them from within multiple ADPs. You can
 also include files and parse other ADPs from within your ADPs.


<br><br>


<br><br>
<li> Tcl Libraries: The alternative to embedding Tcl scripts in HTML
 pages using ADPs, is to store Tcl scripts in Tcl libraries. You
 can define scripts that can be called from ADPs, schedule
 procedures to run at certain times, register scripts to handle
 specific URLs or URL hierarchies, register scripts to handle all
 URLs with a specific file extension, and register filters to be
 run in addition to a URL's registered procedure.


</ul>



<p>


<h2><a name="tcl_interpreters">Tcl Interpreters</a></h2>
<p>


<p>


   During NaviServer initialization, only one interpreter exists. While
   modules are loaded and initialized, they may add procedures to the
   interpreter. When initialization is complete (all modules are loaded
   and all Tcl libraries have been executed), the interpreter may no
   longer be changed.


<p>


   Each connection thread that requires Tcl will create a copy of the
   original interpreter.


<p>



<h2><a name="global_variables">Global Variables</a></h2>
<p>


<p>


   Tcl interpreter has its own global variable table, so global
   variables are shared only within each Tcl interpreter by default. The
   nsv_ commands allows you to make a variable available to all
   interpreters in a virtual server.


<p>


   Global variables are flushed when the Tcl interpreter is deallocated.
   A Tcl interpreter is deallocated explicitly when
   ns_TclDeallocateInterp is called or implicitly at the end of a
   connection. This allows Tcl filter functions to access global
   variables set in ADPs.


<p>


<h2><a name="sharing_files_between_interpreters">Sharing Files Between Interpreters</a></h2>
<p>


<p>


   To share a file between the interpreters in a group, use the Tcl
   detach command. A shared file is left open after an interpreter is
   deallocated.


<p>


   For example:


<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>

    #init time:
    set sharedFile [open myfile.dat]
    detach $sharedFile

</pre></td></tr></table></p>



<p>


   You must manually close the shared file (by performing a close) when
   it is no longer needed or when the server is shut down.


<p>


   Note that there is no implicit locking between interpreters. This
   means that one interpreter can be performing a gets command on the
   shared file at the same time another interpreter performs a close on
   the same file. The results are unpredictable, and could potentially
   cause a core dump. To avoid this situation, use a mutex to protect
   access to the file whenever it is accessed.


<p>


   For example:, at initialization time (for example, in your init.tcl
   script), open a shared file and create a mutex:


<p>



<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>

    set sharedLock [ns_mutex create]
    set sharedFile [open myfile.dat]
    detach $sharedFile

</pre></td></tr></table></p>



<p>


   At run time, use the mutex to protect any actions you perform on the
   shared file:


<p>



<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>

    ns_share sharedLock
    ns_share sharedFile
    ns_mutex lock $sharedLock
    puts $sharedFile ...
    gets $sharedFile ...
    ns_mutex unlock $sharedLock


</pre></td></tr></table></p>



<p>


   At shutdown (for example, in your shutdown procedure registered with
   ns_atshutdown), close the file and destroy the lock:


<p>



<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>

    ns_share sharedLock
    ns_share sharedFile
    close $SharedFile
    ns_mutex destroy $SharedLock

</pre></td></tr></table></p>



<p>


<h2><a name="working_with_ns_set_and_form_data">Working with ns_Set and Form Data</a></h2>
<p>


<p>


   An NaviServer operation procedure often manipulates sets of key-value
   pairs of string data, for example:


<p>


<ul>

<li>rows of data from the database
<br><br>
<li>HTTP header information
<br><br>
<li>HTML form data


</ul>



<p>


    <h3><a name="ns_set_data_structure">ns_Set Data Structure</a></h3>
<p>


<p>


   In the NaviServer C API these data are manipulated using the ns_Set
   data structure. In the Tcl API the ns_set command can be used to
   manipulate underlying ns_Set data structures that are generated by the
   NaviServer communications layer or the database services module.


<p>


   The example below shows a typical use of the ns_set Tcl command to
   manipulate ns_Set structures.


<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>

   # Example 2: Show header data
    #
    # Things to notice:
    # * The same function is registered for two different URLs
    # with different context.
    #
    # * The headers are pulled out of the conn using the
    # ns_conn function.
    #
    # * The value for a particular header line is extracted
    # with &quot;ns_set get&quot;.

    ns_register_proc GET /example/showbrowser showheader USER-AGENT
    ns_register_proc GET /example/showrefer showheader REFER

    proc showheader {conn key} {
          set value [ns_set get [ns_conn headers $conn] $key]
          ns_return $conn 200 text/plain &quot;$key: $value&quot;
    }

</pre></td></tr></table></p>



<p>



<h2><a name="tcl_examples">Tcl Examples</a></h2>
<p>


<h3><a name="example_1:_hello">Example 1: hello</a></h3>
<p>

The following example script implements a simple request procedure
which returns 'Hello World'.


<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>

# Example 1: Hello World
#
# This simple operation just returns a plain text message.
#
# Things to notice:
#
# * ns_register_proc takes as arguments:
#   * the HTTP method
#   * the URL that the procedure handles
#   * the procedure that is executed
#
# * ns_return takes as arguments:
#   * the passed in connection
#   * a return status, in this case 200 for success
#   * a MIME type
#   * the actual string to return
#
# * ns_return properly formats the HTTP response for you.

ns_register_proc GET /example/hello hello

proc hello {conn context} {
   ns_return $conn 200 text/plain &quot;Hello World&quot;
}


</pre></td></tr></table></p>



<h3><a name="example_2:_showhdrs">Example 2: showhdrs</a></h3>
<p>

The following example script shows how to access the HTTP headers sent
by the client within a Tcl script.


<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>


# Example 2: Show header data
#
# Things to notice:
#
# * The same function is registered for two different URLs
#   with different context.
#
# * The headers are pulled out of the conn using the
#   ns_conn function.
#
# * The value for a particular header line is extracted
#   with &quot;ns_set iget&quot;, the case insensitive counterpart to
#   &quot;ns_set get&quot;.

ns_register_proc GET /example/showbrowser showheader USER-AGENT
ns_register_proc GET /example/showrefer showheader REFERER

proc showheader {conn key} {
    set value [ns_set iget [ns_conn headers $conn] $key]
    ns_return $conn 200 text/plain &quot;$key: $value&quot;
}

</pre></td></tr></table></p>



<h3><a name="example_3a:_genstory">Example 3a: genstory</a></h3>
<p>

The following example script provides two request procedures. The
first procedure returns an HTML page for collecting a few fields of
data from the user. The second procedure utilizes the data to generate
a short story.



<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>


# Example 3a: Form generation and handling
#
# Two functions are registered. One generates and
# returns an HTML form, and the other processes
# the data in the form.
#
# Things to notice:
#
# * Different functions are registered to the same
#   URL with different methods.  Note that some browsers
#   do not cache results properly when you do this.
#
# * The genstory function returns an error status
#   (500) if the client doesn't pass in any form data.
#
# * Form data is stored in an ns_set, and accessed
#   like any other set (e.g., header data).
#
# * A counter is used to loop through all the key
#   value pairs in the form.

ns_register_proc GET /example/genstory genstoryform
ns_register_proc POST /example/genstory genstory

proc genstoryform {conn context} {
  ns_return $conn 200 text/html \
&quot;HTML&gt;
&lt;HEAD&gt;
&lt;TITLE&gt;Automatic Story Generator&lt;/TITLE&gt;
&lt;/HEAD&gt;
&lt;BODY&gt;
&lt;H1&gt;
Automatic Story Generator
&lt;/H1&gt;
&lt;FORM ACTION=http:/example/genstory
METHOD=POST&gt;
Noun: &lt;INPUT TYPE=text NAME=noun1&gt;&lt;BR&gt;
Noun: &lt;INPUT TYPE=text NAME=noun2&gt;&lt;BR&gt;
Name: &lt;INPUT TYPE=text NAME=name1&gt;&lt;BR&gt;
Name: &lt;INPUT TYPE=text NAME=name2&gt;&lt;BR&gt;
Adjective: &lt;INPUT TYPE=text NAME=adjective1&gt;&lt;BR&gt;
Adjective: &lt;INPUT TYPE=text NAME=adjective2&gt;&lt;BR&gt;
Verb: &lt;INPUT TYPE=text NAME=verb1&gt;&lt;BR&gt;
Verb: &lt;INPUT TYPE=text NAME=verb2&gt;&lt;BR&gt;
&lt;P&gt;&lt;INPUT TYPE=submit VALUE=\&quot;Generate\&quot;&gt;
&lt;/FORM&gt;
&lt;P&gt;
&lt;/BODY&gt;&lt;/HTML&gt;
&quot;}

proc genstory {conn ignore} {
  set formdata [ns_conn form $conn]

  if {$formdata == &quot;&quot;} {
    ns_return $conn 200 text/plain &quot;Need form data!&quot;
    return
  }

  # Build up a human-readable representation of the form data.

  set hrformdata &quot;&lt;dl&gt;&quot;
  set size [ns_set size $formdata]
  for {set i 0} {$i &lt; $size} {incr i} {
    append hrformdata &quot;&lt;dt&gt;[ns_set key $formdata $i]&lt;/dt&gt;&lt;dd&gt;[ns_set value $formdata $i]&lt;/dd&gt;&quot;
  }
  append hrformdata &quot;&lt;/dl&gt;&quot;

  ns_return $conn 200 text/html \
&quot;&lt;HTML&gt;
&lt;HEAD&gt;
&lt;TITLE&gt;The story of [ns_set get $formdata name1] and
[ns_set get $formdata name2]&lt;/TITLE&gt;
&lt;/HEAD&gt;
&lt;BODY&gt;
&lt;H1&gt;
The story of [ns_set get $formdata name1] and [ns_set get $formdata name2]
&lt;/H1&gt;
&lt;P&gt;Once upon a time [ns_set get $formdata name1] and
[ns_set get $formdata name2] went for a
walk in the woods looking for a [ns_set get $formdata noun1].
[ns_set get $formdata name1] was
feeling [ns_set get $formdata adjective1] because
[ns_set get $formdata name2] was so
[ns_set get $formdata adjective2].  So
[ns_set get $formdata name1] decided to
[ns_set get $formdata verb1] [ns_set get $formdata name2]
with a [ns_set get $formdata noun2].  This made
[ns_set get $formdata name2] [ns_set get $formdata verb2]
[ns_set get $formdata name1].
&lt;P&gt;&lt;CENTER&gt;The End&lt;/CENTER&gt;
The form data that made this possible:&lt;BR&gt;
$hrformdata
&lt;/BODY&gt;&lt;/HTML&gt;&quot;
}


</pre></td></tr></table></p>



<h3><a name="example_3b:_pagetcl/genstory">Example 3b: pagetcl/genstory</a></h3>
<p>

The following example script implements the same story generating
function of genstory (the previous example) but is implemented as a
page Tcl script instead of a library Tcl script. Note that the
associated HTML file (genstory.htm) is also included after the Tcl
script.


<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>


# Example 3b: Form generation and handling
#
# This operation generates a story based on the
# form data submitted from the form genstory.htm.
#
# Things to notice:
#
# * This file should be stored with the HTML pages
#   of the server.  When a client requests the URL corresponding
#   to the file, the NaviServer sets the &quot;conn&quot; variable and
#   evaluates the Tcl.
#
# * An error status (500) is returned if the client doesn't
#   doesn't pass in any form data.
#
# * Form data is stored in an ns_set, and accessed
#   like any other set (e.g., header data).
#
# * A counter is used to loop through all the key
#   value pairs in the form.

set formdata [ns_conn form $conn]

if {$formdata == &quot;&quot;} {
    ns_return $conn 200 text/plain &quot;Need form data!&quot;
    return
}

# Build up a human-readable representation of the form data.

set hrformdata &quot;&lt;dl&gt;&quot;
set size [ns_set size $formdata]
for {set i 0} {$i &lt; $size} {incr i} {
    append hrformdata &quot;&lt;dt&gt;[ns_set key $formdata $i]&lt;/dt&gt;\
        &lt;dd&gt;[ns_set value $formdata $i]&lt;/dd&gt;&quot;
}
append hrformdata &quot;&lt;/dl&gt;&quot;

ns_return $conn 200 text/html \
&quot;&lt;HTML&gt;
&lt;HEAD&gt;
&lt;TITLE&gt;The story of [ns_set get $formdata name1] and
[ns_set get $formdata name2]&lt;/TITLE&gt;
&lt;/HEAD&gt;
&lt;BODY&gt;
&lt;H1&gt;
The story of [ns_set get $formdata name1] and
[ns_set get $formdata name2]
&lt;/H1&gt;
&lt;P&gt;Once upon a time [ns_set get $formdata name1] and
[ns_set get $formdata name2] went for a
walk in the woods looking for a [ns_set get $formdata noun1].
[ns_set get $formdata name1] was
feeling [ns_set get $formdata adjective1] because
[ns_set get $formdata name2] was so
[ns_set get $formdata adjective2].  So
[ns_set get $formdata name1] decided to
[ns_set get $formdata verb1] [ns_set get $formdata name2]
with a [ns_set get $formdata noun2].  This made
[ns_set get $formdata name2] [ns_set get $formdata verb2]
[ns_set get $formdata name1].
&lt;P&gt;&lt;CENTER&gt;The End&lt;/CENTER&gt;
The form data that made this possible:&lt;BR&gt;
$hrformdata
&lt;/BODY&gt;&lt;/HTML&gt;&quot;

Here's the associated HTML file:

&lt;HTML&gt;
&lt;HEAD&gt;
&lt;TITLE&gt;Automatic Story Generator&lt;/TITLE&gt;
&lt;/HEAD&gt;
&lt;BODY&gt;
&lt;H1&gt;
Automatic Story Generator
&lt;/H1&gt;
&lt;FORM ACTION=genstory.tcl METHOD=POST&gt;
Noun: &lt;INPUT TYPE=text NAME=noun1&gt;&lt;BR&gt;
Noun: &lt;INPUT TYPE=text NAME=noun2&gt;&lt;BR&gt;
Name: &lt;INPUT TYPE=text NAME=name1&gt;&lt;BR&gt;
Name: &lt;INPUT TYPE=text NAME=name2&gt;&lt;BR&gt;
Adjective: &lt;INPUT TYPE=text NAME=adjective1&gt;&lt;BR&gt;
Adjective: &lt;INPUT TYPE=text NAME=adjective2&gt;&lt;BR&gt;
Verb: &lt;INPUT TYPE=text NAME=verb1&gt;&lt;BR&gt;
Verb: &lt;INPUT TYPE=text NAME=verb2&gt;&lt;BR&gt;
&lt;P&gt;&lt;INPUT TYPE=submit VALUE=&quot;Generate&quot;&gt;
&lt;/FORM&gt;
&lt;P&gt;
&lt;/BODY&gt;&lt;/HTML&gt;


</pre></td></tr></table></p>



<h3><a name="example_4:_redirect">Example 4: redirect</a></h3>
<p>

The following example script shows how to use an NaviServer simple
response command (in this case, ns_returnredirect) and the equivalent
code when sending raw data to the client.



<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>


# Example 4: Implementing redirects with ns_respond and
# ns_write
#
# /example/not_here uses ns_respond to return an HTTP
#   redirect to /example/finaldest.
# /example/not_here2 does the same thing using ns_write
# /example/not_here3 does the same thing with
#   ns_returnredirect
#
# Things to notice:
#
# * When you use ns_write, you need to compose the
#   entire response.
#
# * &quot;ns_conn location&quot; returns the http://hostname
#   part of the URL that you can use to generate
#   fully qualified URLs.
#
# * ns_returnredirect is a lot simpler than either
#   ns_respond or ns_write.

ns_register_proc GET /example/finaldest finaldest
ns_register_proc GET /example/not_here not_here
ns_register_proc GET /example/not_here2 not_here2
ns_register_proc GET /example/not_here3 not_here3

proc not_here {conn ignore} {
  set headers [ns_set new myheaders]
  ns_set put $headers Location \
      [ns_conn location $conn]/example/finaldest
  ns_respond $conn -status 302 -type text/plain \
      -string &quot;Redirection&quot; -headers $headers
}

proc not_here2 {conn context} {
  set content \
&quot;&lt;HTML&gt;&lt;HEAD&gt;&lt;TITLE&gt;Redirection&lt;/TITLE&gt;&lt;/HEAD&gt;&lt;BODY&gt;
&lt;H1&gt;Redirection&lt;/H1&gt;The actual location of what you were looking for is
&lt;A HREF=\&quot;[ns_conn location $conn]/example/finaldest\&quot;&gt; here.&lt;/A&gt;
&lt;/BODY&gt;&lt;/HTML&gt;&quot;

  ns_write $conn \
&quot;HTTP/1.0 302 Document follows
MIME-Version: 1.0
Content-Type: text/html
Content-Length: [string length $content]
Location: [ns_conn location $conn]/example/finaldest

$content&quot;
}

proc finaldest {conn context} {
  ns_return $conn 200 text/plain \
      &quot;You have arrived at the final destination.&quot;
}

proc not_here3 {conn context} {
  ns_returnredirect $conn \
      [ns_conn location $conn]/example/finaldest
}

</pre></td></tr></table></p>


<h3><a name="example_5:_desctable">Example 5: desctable</a></h3>
<p>

The following example script provides a request procedure which
describes the columns of a database table using the NaviServer
&quot;ns_tableinfo&quot; command .



<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>


# Example 5: Describing a database table
#
# /example/describetable prints out a column-by-column
#   description of a database table.  The database
#   pool name and table name are specified at the end
#   of the URL -- e.g.,
#
#       /example/describetable/nsdbpool/ns_users
#
# Note: You must have the ns_db module loaded into your virtual
#       server for this example to work.
#
# Things to notice:
#
# * ns_returnbadrequest returns a nicely formatted message
#   telling the client they submitted an invalid request.
#
# * &quot;ns_conn urlv&quot; returns a Tcl array whose elements are the
#   slash-delimited parts of the URL.
#
# * The describetable function loops through all the columns
#   and uses &quot;ns_column valuebyindex&quot; to get the type of each
#   one.
#
# * ns_returnnotice nicely formats the return value.

ns_register_proc GET /example/describetable describetable

proc describetable {conn ignore} {
    if {[ns_conn urlc $conn] != 4} {
    return [ns_returnbadrequest $conn &quot;Missing table name and/or poolname&quot;]
    }
    set pool [lindex [ns_conn urlv $conn] 2]
    if {[lsearch $pool [ns_db pools]] == -1} {
    return [ns_returnbadrequest $conn &quot;Pool $pool does not exist&quot;]
    }
    set db [ns_db gethandle $pool]
    set table [lindex [ns_conn urlv $conn] 3]
    set tinfo [ns_table info $db $table]
    if {$tinfo == &quot;&quot;} {
    return [ns_returnbadrequest $conn &quot;Table $table does not exist&quot;]
    }
    set output &quot;&lt;dl&gt;&quot;
    set size [ns_column count $tinfo]
    for {set i 0} {$i &lt; $size} {incr i} {
    append output &quot;&lt;dt&gt;[ns_column name $tinfo $i] \
                &lt;dd&gt;[ns_column typebyindex $tinfo $i]&lt;/dd&gt;&quot;
    }
    append output &quot;&lt;/dl&gt;&lt;hr&gt;&quot;
    ns_returnnotice $conn 200 &quot;Table $table in pool $pool&quot; $output
}


</pre></td></tr></table></p>
.


<h3><a name="example_6:_getempsm">Example 6: getempsm</a></h3>
<p>

The following example script shows how to query a table in the
database.



<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>


# Example 6: Getting data from the database
#
# /example/getemps queries a database in the default
# pool and returns a list of all the employees listed
# in the employees table.  It assumes a table called
# employees exists with the column emp_name. You can
# use the /NS/Db/Admin to create the table.
#
# Note: You must have the ns_db module loaded into your virtual
#       server for this example to work.
#
# Things to notice:
#
# * Use &quot;ns_db gethandle&quot; to get a handle for the database
#   from the default database pool of the virtual server.
#
# * Use &quot;ns_db select&quot; to query the database and
#   &quot;ns_db getrow&quot; to retrieve data.
#
# * Rows are returned as ns_sets.
#

ns_register_proc GET /example/getemps getemps

proc getemps {conn context} {
        set ul &quot;&lt;UL&gt;&quot;
        set db [ns_db gethandle [ns_config [ns_dbconfigpath] &quot;DefaultPool&quot;]]
        set row [ns_db select $db &quot;select emp_name from employees order by emp_name;&quot;]
        while { [ns_db getrow $db $row] } {
            append ul &quot;&lt;LI&gt;[ns_set get $row emp_name] \n&quot;
        }
        append ul &quot;&lt;/UL&gt;&quot;
        ns_returnnotice $conn 200 &quot;Employee list&quot; $ul
}



</pre></td></tr></table></p>



<h3><a name="example_7:_wincgi">Example 7: wincgi</a></h3>
<p>

The following example script is a simple emulation of the WebSite
WinCGI interface.



<p><table><tr><td bgcolor=black>&nbsp;</td><td><pre class='sample'>
.

#
# Example 7: simple emulation of the WebSite WinCGI interface
#
# This Tcl script emulates the WinCGI interface of the WebSite server.
# To use, move this file to your Tcl library directory (normally the
# modules/tcl directory of the NaviServer directory), set the
# following nsd.ini variables in the [ns\server\&lt;server-name&gt;\wincgi]
# section, and restart the server.
#
# key     default    description
# ---     -------    -----------
# prefix  /cgi-win   URL prefix for WinCGI.
# debug   off        Set to on to keep temp files for debugging.
# gmtoff  0          Minutes West of GMT for the &quot;GMT Offset&quot; variable.
# dir     c:\wincgi  Directory of WinCGI programs.
#
#
# Note:  This script is unsupported and not a complete emulation of the
# WebSite WinCGI interface.  In particular, not all the WinCGI variables
# are set.  Full support for WinCGI will be incorporated into the nscgi
# module in a future NaviServer release.
#

#
# Fetch the variables from the configuration file.
#
global WinCGI
set WinCGI(section) &quot;ns\\server\\[ns_info server]\\wincgi&quot;
if {[set WinCGI(prefix) [ns_config $WinCGI(section) prefix]] == &quot;&quot;} {
    set WinCGI(prefix) /cgi-win
}
if {[set WinCGI(dir) [ns_config $WinCGI(section) dir]] == &quot;&quot;} {
    set WinCGI(dir) [ns_info home]/$WinCGI(prefix)
}
if {[set WinCGI(gmtoff) [ns_config $WinCGI(section) gmtoff]] == &quot;&quot;} {
    set WinCGI(gmtoff) 0
}
if {[set WinCGI(debug) [ns_config -bool $WinCGI(section) debug]] == &quot;&quot;} {
    set WinCGI(debug) 0
}


#
# Register the win-cgi procedure to handle requests for WinCGI executables.
#
ns_register_proc POST $WinCGI(prefix)/*.exe win-cgi
ns_register_proc GET $WinCGI(prefix)/*.exe win-cgi


#
# win-cgi - The Tcl request procedure which emulates WinCGI.
#
proc win-cgi {conn ignored} {
    global WinCGI

    # The program is the second part of the WinCGI URL.
    set args [join [split [ns_conn query $conn] &amp;]]
    set pgm [lindex [ns_conn urlv $conn] 1]
    regsub -all {\+} $args &quot; &quot; args
    foreach e [split $WinCGI(dir)/$pgm /] {
            if {$e != &quot;&quot;} {lappend exec $e}
    }
    set exec [join $exec ]
    if ![file executable $exec] {
            return [ns_returnnotfound $conn]
    }

    # WinCGI requires a few temporary files.
    set ini [ns_tmpnam]
    set inp [ns_tmpnam]
    set out [ns_tmpnam]

    # Copy the request content to the input file.
    set fp [open $inp w]
    ns_writecontent $conn $fp
    set len [tell $fp]
    close $fp

    # Create the WinCGI variables .ini file.
    set fp [open $ini w]
    puts $fp {[CGI]}
    puts $fp \
&quot;Request Protocol=HTTP/1.0
Request Method=[ns_conn method $conn]
Executable Path=$WinCGI(prefix)/$pgm
Server Software=[ns_info name]/[ns_info version]
Server Name=[ns_info name]
Server Port=[ns_info version]
Server Admin=[ns_config NaviServer WebMaster]
CGI Version=CGI/1.2 (Win)
Remote Address=[ns_conn peeraddr $conn]
Authentication Method=Basic
Authentication Realm=[ns_conn location $conn]
Content Type=application/x-www-form-urlencoded
Content Length=$len&quot;
    puts $fp &quot;&quot;
    puts $fp {[System]}
    puts $fp \
&quot;GMT Offset=$WinCGI(gmtoff)
Debug Mode=Yes
Output File=$out
Content File=$inp&quot;

    # Set any POST or query form variables.
    puts $fp &quot;&quot;
    puts $fp {[Form Literal]}
    set form [ns_conn form $conn]
    if {$form != &quot;&quot;} {
            for {set i 0} {$i &lt; [ns_set size $form]} {incr i} {
                    set key [ns_set key $form $i]
                    set value [ns_set value $form $i]
                    puts $fp &quot;$key=$value&quot;
            }
    }

    # Set the accept headers and accumulate the extra headers.
    puts $fp &quot;&quot;
    puts $fp {[Accept]}
    set headers [ns_conn headers $conn]
    set extras &quot;&quot;
    for {set i 0} {$i &lt; [ns_set size $headers]} {incr i} {
            set key [ns_set key $headers $i]
            set ukey [string toupper $key]
            set value [ns_set value $headers $i]
            if {$ukey == &quot;ACCEPT&quot;} {
                    puts $fp &quot;$value=Yes&quot;
            } elseif {$key != &quot;CONTENT-LENGTH&quot; &amp;&amp; $key != &quot;CONTENT-TYPE&quot;} {
                    append extras &quot;$key=$value\n&quot;
            }
    }
    puts $fp &quot;&quot;
    puts $fp {[Extra Headers]}
    puts $fp $extras
    close $fp

    # Execute the WinCGI program.
    # NB:  &quot;catch&quot; the exec and open because a WinCGI
    # program can be misbehaved, returning a non-zero
    # exit status or not creating the output file.
    catch {exec &quot;$exec $ini $inp $out $args&quot;}
    if [catch {set fp [open $out]}] {
            ns_returnerror $conn 500 &quot;WinCGI exec failed&quot;
    } else {
            set type text/html
            set status 200
            while {[gets $fp line] &gt; 0} {
                    set line [string trim $line]
                    if {$line == &quot;&quot;} break
                    set head [split $line :]
                    set key [string tolower [string trim [lindex $head 0]]]
                    set value [string trim [lindex $head 1]]
                    if {$key == &quot;content-type&quot;} {
                            set type $value
                    } elseif {$key == &quot;location&quot;} {
                            set location $value
                    } elseif {$key == &quot;status&quot;} {
                            set status $status
                    }
            }
            set page [read $fp]
            close $fp

            if [info exists location] {
                    ns_returnredirect $conn $location
            } else {
                    ns_return $conn $status $type $page
            }
    }

    if $WinCGI(debug) {
            ns_log Notice &quot;CGI $pgm: ini: $ini, inp: $inp, out: $out&quot;
    } else {
            ns_unlink -nocomplain $ini
            ns_unlink -nocomplain $inp
            ns_unlink -nocomplain $out
    }
}


</pre></td></tr></table></p>


</body></html>
